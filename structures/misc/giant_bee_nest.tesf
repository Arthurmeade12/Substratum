num radius = 4+randomInt(3);
num random = randomInt(3);
str setBlock = "minecraft:honeycomb_block";

for (num y = -30; y < 30; y = y + 1) {
    for (num x = -radius; x < radius; x = x + 1) {
        for (num z = -radius; z < radius; z = z + 1) {
            if ((pow(x,2)*(3+random))+(pow(y,2)/(3+random))+(pow(z,2)*(3+random)) < pow(radius,2)) {

                if (randomInt(2) == 0) {
                    structure(x, -y, z, "bee_nest_block","NONE"); 
                } else {
                    setBlock = "minecraft:honeycomb_block";
                    if (randomInt(3) == 0) setBlock = "minecraft:honey_block";
                    block(x, -y, z, setBlock);
                }

            }  
        }
    }
}

entity(4, 0, 0, "minecraft:bee");
entity(0, 0, 4, "minecraft:bee");
entity(-4, 0, 0, "minecraft:bee");

